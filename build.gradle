group = 'com.github.ganet'
version = '0.1.1-SNAPSHOT'
description = "RxJava2 wrapper for aerospike-client-java"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.aerospike:aerospike-client:$aerospike_client_version"
    compile "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    testCompile "junit:junit:$junit_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    doLast {
        signing {
            sign configurations.archives
        }

        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)

                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name = 'RxAerospike'
                    packaging = 'jar'
                    description = 'RxJava2 wrapper for aerospike-client-java'
                    url = 'https://github.com/Ganet/rxaerospike'

                    scm {
                        connection = 'scm:git:git://github.com/Ganet/rxaerospike.git'
                        developerConnection = 'scm:git:ssh://github.com/Ganet/rxaerospike.git'
                        url = 'https://github.com/Ganet/rxaerospike/tree/master'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'Ganet'
                            name = 'Jose Ignacio Acin Pozo'
                            email = 'joseignacio.acinpozo@gmail.com'
                            organization = 'Github'
                            organizationUrl = 'https://github.com/Ganet'
                        }
                    }
                }
            }
        }
    }

}

